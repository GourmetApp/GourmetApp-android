apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    signingConfigs {
        releaseConfig {
            if (rootProject.file('keystore.properties').exists()) {
                def props = new Properties()
                props.load(new FileInputStream(rootProject.file('keystore.properties')))

                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            } else {
                storeFile = file(System.getenv('TRAVIS_STORE_FILE'))
                storePassword = System.getenv('TRAVIS_STORE_PASSWORD')
                keyAlias System.getenv('TRAVIS_ALIAS_KEY')
                keyPassword = System.getenv('TRAVIS_ALIAS_PASSWORD')
            }
        }
    }

    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    defaultConfig {
        applicationId "com.jugarte.gourmet"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 10
        versionName "1.5.0"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def apk = output.outputFile;
                    def newName = "GourmetApp" + "-v" + defaultConfig.versionName + ".apk";
                    output.outputFile = new File(apk.parentFile, newName);
                }
            }
        }
    }
}

dependencies {
    compile project(':gourmet')

    def supportLibVersion = '25.3.1'
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:support-v4:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:percent:$supportLibVersion"

    compile 'com.github.javierugarte:searchtoolbar:1.0.2'

    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'

    compile 'br.com.simplepass:loading-button-android:1.7.1'

    compile 'com.github.wrdlbrnft:sorted-list-adapter:0.2.0.19'

    def dagger2Version = '2.8'
    compile "com.google.dagger:dagger:$dagger2Version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger2Version"

    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }

    // Wear module:
    //compile project(':wear-http')
    //compile 'com.kogitune:wear-http:0.0.7'
    //debugWearApp project(path:':wear', configuration: 'debug')
    //releaseWearApp project(path:':wear', configuration: 'release')

}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    signingConfigs {
        releaseConfig {
            if (rootProject.file('keystore.properties').exists()) {
                def props = new Properties()
                props.load(new FileInputStream(rootProject.file('keystore.properties')))

                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            } else {
                storeFile = file(System.getenv('TRAVIS_STORE_FILE'))
                storePassword = System.getenv('TRAVIS_STORE_PASSWORD')
                keyAlias = System.getenv('TRAVIS_ALIAS_KEY')
                keyPassword = System.getenv('TRAVIS_ALIAS_PASSWORD')
            }
        }
    }

    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.jugarte.gourmet"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 15
        versionName "2.0.2"

        vectorDrawables.useSupportLibrary = true

    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def versionName = variant.versionName
                    outputFileName = "GourmetApp" + "-v" + versionName + ".apk"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    api project(':gourmet')

    def supportLibVersion = '28.0.0'
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:percent:$supportLibVersion"

    implementation 'com.github.javierugarte:searchtoolbar:1.0.2'

    implementation 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
    implementation 'br.com.simplepass:loading-button-android:1.7.1'

    implementation 'com.github.wrdlbrnft:sorted-list-adapter:0.2.0.19'

    def dagger2Version = '2.8'
    implementation "com.google.dagger:dagger:$dagger2Version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger2Version"

    implementation 'com.brandongogetap:stickyheaders:0.5.1'

    implementation('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }

    // RX
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation "io.reactivex.rxjava2:rxjava:2.1.5"

    implementation 'com.google.firebase:firebase-database:10.2.4'

}

apply plugin: 'com.google.gms.google-services'